#version 430 core
layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

// 粒子データ
struct Particle
{
  vec4 position;
  vec4 velocity;
};

// 粒子群データ
layout(std430, binding = 0) buffer Particles
{
  Particle particle[];
};

// 重力加速度
uniform vec4 gravity = vec4(0.0, -9.8, 0.0, 0.0);

// 地面の高さ
uniform float height = -1.5;

// 減衰率
uniform float attenuation = 0.7;

// タイムステップ
uniform float dt = 0.01666667;

void main()
{
  // ワークグループ ID をのまま頂点データのインデックスに使う
  const uint i = gl_WorkGroupID.x;

  // 速度を更新する
  particle[i].velocity += gravity * dt;

  // 位置を更新する
  particle[i].position += particle[i].velocity * dt;

  // もし地面に落ちたら
  if (particle[i].position.y < height)
  {
    // y 方向の速度を反転する
    particle[i].velocity.y = -attenuation * particle[i].velocity.y;

    // 高さを地面の高さに戻す
    particle[i].position.y = height;
  }
}
